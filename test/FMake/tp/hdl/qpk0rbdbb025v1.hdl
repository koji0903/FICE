/**********************************************************************/
/*                                                                    */
/* K0R series PORT bit macro                                          */
/*                                                                    */
/*   [ port bit : 入出力ポート ]                                      */
/*                                                                    */
/*    PL,PM,PU,PIM,POM,PMC あり                                       */
/*    兼用出力機能 あり                                               */
/*    兼用入力機能 あり                                               */
/*                                                                    */
/*    Module   : QPK0RBDBB025V1                                       */
/*    Date     : 2009/11/27                                           */
/*    Revision : 1.00                                                 */
/*    Designer : S.Nakata                                             */
/*    Note     :                                                      */
/*                                                                    */
/**********************************************************************/
/*                                                                    */
/* <<入力信号>>                                         <<接続先>>    */
/* PCLKRW      : 周辺マクロ用 BASECLK                   CG            */
/* PRESETZ     : 周辺マクロ用リセット信号               RESETCTL      */
/* PWDATA      : 入力データバス                         APB Bridge    */
/* RE_PORT     : PORTリードイネーブル信号               port control  */
/* WE_PL       : PLレジスタライトイネーブル信号         port control  */
/* WE_PM       : PMレジスタライトイネーブル信号         port control  */
/* WE_PU       : PUレジスタライトイネーブル信号         port control  */
/* WE_PIM      : PIMレジスタライトイネーブル信号        port control  */
/* WE_POM      : POMレジスタライトイネーブル信号        port control  */
/* WE_PMC      : PIMレジスタライトイネーブル信号        port control  */
/* SEL_PL      : PL Register select 信号                port control  */
/* SEL_PM      : PM Register select 信号                port control  */
/* SEL_PU      : PU Register select 信号                port control  */
/* SEL_PIM     : PIM Register select 信号               port control  */
/* SEL_PMC     : PIM Register select 信号               port control  */
/* SEL_POM     : POM Register select 信号               port control  */
/* OPEN_BUF    : 入力バッファイネーブル信号             port control  */
/* DIN         : ポートレベル入力信号                   buffer        */
/* EXOUTA      : 兼用機能出力信号                                     */
/* EXOUTB      : 兼用機能出力信号                                     */
/* EXOUTC      : 兼用機能出力信号                                     */
/*                                                                    */
/* SCANMODE    : SCANテストモード信号                   MODECTL       */
/* TESDBT      : BTモード信号                           MODECTL       */
/* OPTOPLRD    : OPTOPLRDモード信号                     MODECTL       */
/*                                                                    */
/* <<出力信号>>                                         <<接続先>>    */
/* PRDATA      : 出力データバス                         APB Bridge    */
/* PUON        : プルアップ抵抗制御信号                 buffer        */
/* ENO         : 出力イネーブル信号                     buffer        */
/* ENI         : 入力イネーブル信号                     buffer        */
/* DOUT        : 出力データ信号                         buffer        */
/* SELIN       : 入力バッファ選択信号                   buffer        */
/* EXINA       : 兼用機能入力信号                                     */
/*                                                                    */
/*                                                                    */
/**********************************************************************/

module QPK0RBDBB025V1 ( PCLKRW   , PRESETZ  , PWDATA   , RE_PORT  ,
                        WE_PL    , WE_PM    , WE_PU    , WE_PIM   , WE_POM   , WE_PMC   ,
			SEL_PL   , SEL_PM   , SEL_PU   , SEL_PIM  , SEL_POM  , SEL_PMC  ,
                        OPEN_BUF , DIN      , EXOUTA   , EXOUTB   , EXOUTC   ,
			SCANMODE , TESDBT   , OPTOPLRD ,
                        PRDATA   , PUON     , ENO      , ENI      , DOUT     ,
			SELIN    , EXINA    , din_exor ) ;


	input		PCLKRW   , PRESETZ  , PWDATA   , RE_PORT  ;
	input		WE_PL    , WE_PM    , WE_PU    , WE_PIM   , WE_POM   , WE_PMC   ;
	input		SEL_PL   , SEL_PM   , SEL_PU   , SEL_PIM  , SEL_POM  , SEL_PMC  ;
	input		OPEN_BUF , DIN      , EXOUTA   , EXOUTB   , EXOUTC    ;
	input		SCANMODE , TESDBT   , OPTOPLRD ;

	output		PRDATA   , PUON     , ENO      , ENI      , DOUT     ;
	output		SELIN    , EXINA    ;
        output          din_exor ;

	reg		PL       , PM       , PU       , PIM      , POM      , PMC      , READ_DATA ;
	reg		ENO_s    , PUON_s   , ENI_s    , DIN_s    , RDPT     ;

	wire		PUON_pre , ENI_pre  , ENO_pre  ;
        wire            ANDOUT   ;
	wire		PRDATA   ;
	wire		DOUT     , SELIN    , EXINA    , DIN      ;
        wire            din_exor ;

//=====  port register write access  =====

        //      synopsys async_set_reset "PRESETZ"
	always @ ( posedge PCLKRW or negedge PRESETZ ) begin
	    if ( ! PRESETZ )
                PL <= 1'b0 ;
	    else if ( WE_PL )
                PL <= PWDATA ;
            else
                PL <= PL ;
        end

        //      synopsys async_set_reset "PRESETZ"
	always @ ( posedge PCLKRW or negedge PRESETZ ) begin
	    if ( ! PRESETZ ) 
                PM <= 1'b1 ;
	    else if ( WE_PM )
                PM <= PWDATA ;
            else
                PM <= PM ;
        end

        //      synopsys async_set_reset "PRESETZ"
	always @ ( posedge PCLKRW or negedge PRESETZ ) begin
	    if ( ! PRESETZ ) 
                PU <= 1'b0 ;
	    else if ( WE_PU )
                PU <= PWDATA ;
            else
                PU <= PU ;
        end

        //      synopsys async_set_reset "PRESETZ"
	always @ ( posedge PCLKRW or negedge PRESETZ ) begin
	    if ( ! PRESETZ ) 
                PIM <= 1'b0 ;
	    else if ( WE_PIM )
                PIM <= PWDATA ;
            else
                PIM <= PIM ;
        end

        //      synopsys async_set_reset "PRESETZ"
        always @ ( posedge PCLKRW or negedge PRESETZ ) begin
            if ( ! PRESETZ ) 
                POM <= 1'b0 ;
            else if ( WE_POM )
                POM <= PWDATA ;
            else
                POM <= POM ;
        end

        //      synopsys async_set_reset "PRESETZ"
        always @ ( posedge PCLKRW or negedge PRESETZ ) begin
            if ( ! PRESETZ )
                PMC <= 1'b1 ;
            else if ( WE_PMC )
                PMC <= PWDATA ;
            else
                PMC <= PMC ;
        end

//=====  port register read access  =====

        always @ ( PM or OPTOPLRD or DIN_s or PL ) begin
            case ( { PM , OPTOPLRD } )  // synopsys parallel_case
                2'b00   : RDPT  =  PL ;         // port output mode
                2'b10   : RDPT  =  DIN_s ;      // port input mode
                2'b01   : RDPT  =  DIN_s ;      // optoplrd mode
                2'b11   : RDPT  =  DIN_s ;      // optoplrd mode
            endcase
        end

	always @ ( SEL_PIM or SEL_POM or SEL_PL or SEL_PM or SEL_PU or SEL_PMC or RDPT or PM or PU or PIM or POM or PMC ) begin
	    case ( { SEL_PIM , SEL_POM , SEL_PL , SEL_PM , SEL_PU , SEL_PMC } )	// synopsys parallel_case
		6'b000001 : READ_DATA = PMC ;
		6'b000010 : READ_DATA = PU ;
		6'b000100 : READ_DATA = PM ;
		6'b001000 : READ_DATA = RDPT ;
		6'b010000 : READ_DATA = POM ;
		6'b100000 : READ_DATA = PIM ;
		default   : READ_DATA = 1'b0 ;
	    endcase
	end

	assign PRDATA = READ_DATA & RE_PORT ;

//=====  port function  =====

        assign ANDOUT   = PL & EXOUTA ;
        assign DOUT     = ANDOUT | EXOUTB | EXOUTC ;

        assign ENO_pre	= ( POM )? ( ~PM & ~PMC & ~DOUT ) : ~PM & ~PMC ;
        assign ENI_pre	= OPEN_BUF & ~PMC ;
	assign PUON_pre	= PU & PM & ~PMC & ~POM ;
	assign SELIN	= PIM & ~SCANMODE ;	
	assign EXINA	= DIN_s ;
	assign ENO	= ENO_s ;
	assign ENI	= ENI_s ;
	assign PUON     = PUON_s ;

	assign din_exor = PUON_pre ^ ENO_pre ^ DOUT ^ ENI_pre ;

        always@ ( SCANMODE or TESDBT or ENO_pre ) begin
            case ( { SCANMODE, TESDBT } )       // synopsys parallel_case
                2'b00   : ENO_s = ENO_pre ;       // user mode
                2'b10   : ENO_s = 1'b0 ;          // scan mode
                2'b11   : ENO_s = 1'b1 ;          // bt mode
                default : ENO_s = ENO_pre ;
            endcase
        end

        always@ ( SCANMODE or TESDBT or ENI_pre ) begin
            case ( { SCANMODE, TESDBT } )       // synopsys parallel_case
                2'b00   : ENI_s = ENI_pre ;       // user mode
                2'b10   : ENI_s = 1'b0 ;          // scan mode
                2'b11   : ENI_s = 1'b1 ;          // bt mode
                default : ENI_s = ENI_pre ;
            endcase
        end

        always@ ( SCANMODE or PUON_pre ) begin
            case ( SCANMODE )			// synopsys parallel_case
                1'b0    : PUON_s = PUON_pre ;     // user mode
                1'b1    : PUON_s = 1'b0 ;         // scan/bt mode
            endcase
        end

        always@ ( SCANMODE or TESDBT or DIN or din_exor ) begin
            case ( { SCANMODE, TESDBT } )       // synopsys parallel_case
                2'b00   : DIN_s = DIN ;         // user mode
                2'b10   : DIN_s = din_exor ;    // scan mode
                2'b11   : DIN_s = DIN ;         // bt mode
                default : DIN_s = DIN ;
            endcase
        end

endmodule
